grammar org.applause.MobDsl with org.eclipse.xtext.common.Terminals

generate mobDsl "http://www.applause.org/MobDsl"

Model returns Namespace:
	(imports+=Import)*
	(elements+=ModelElement)*;
	
ModelElement:
	Namespace | Type;
	
Namespace:
	'namespace' name=FQN '{'
		(imports+=Import)*
		(elements+=ModelElement)*
	'}';

FQN:
	ID ('.' ID)*;

Import :
	'import' importedNamespace=ImportedFQN;

ImportedFQN:
	FQN ('.' '*')?;

Type:
	SimpleType | Entity;
	
SimpleType:
	'type' name=ID;
	
Entity :
	'entity' name=ID ('extends' extends=[Entity|FQN])? '{'
		properties+=Property*
	'}';

Property:
	'property' name=ID ':' type=[Type|FQN] (many?='[]')?;
